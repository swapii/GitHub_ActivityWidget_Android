plugins {
	id 'com.android.application'
	id 'kotlin-android'
	id 'org.jetbrains.kotlin.kapt'
}

android {

	compileSdkVersion 25
	buildToolsVersion '25.0.0'

	defaultConfig {
		applicationId 'github.activity'
		minSdkVersion 16
		targetSdkVersion 25
		versionCode project.versionCode as int
		versionName '1.0.0'
	}

	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}
	}

	applicationVariants.all { variant ->
		variant.outputs.all {
			outputFileName = outputFileName
				.replace('app-', 'GithubActivityWidget-')
				.replace('.apk', "-${variant.versionName}-b${variant.versionCode}.apk")
				.replace('-release', '')
		}
	}

}

kapt {
	arguments {
		arg('androidManifestFile', android.applicationVariants[0].outputs[0].processResources.manifestFile)
		arg('resourcePackageName', android.defaultConfig.applicationId)
	}
}

dependencies {

	implementation project(':dao')
	implementation project(':github')

	kapt 'org.androidannotations:androidannotations:3.1'
	implementation 'org.androidannotations:androidannotations-api:3.1'

	implementation 'com.android.support:appcompat-v7:25.0.1'
	implementation 'com.android.support:gridlayout-v7:25.0.1'

	implementation 'com.noveogroup.android:android-logger:1.3.5'

	implementation 'org.apache.commons:commons-lang3:3.5'

}
